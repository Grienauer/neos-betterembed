include: Component/**/*

prototype(Neos.Neos:Page) {
    head {
        stylesheets {
            betterEmbed = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel="stylesheet"
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Ds.BetterEmbed/Public/css/defaultStyle.css'
                    }
                }
            }

            betterEmbedPlaceholder = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel="stylesheet"
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Ds.BetterEmbed/Public/css/embed.css'
                    }
                }
            }
        }
    }
}


prototype(Ds.BetterEmbed:Item) < prototype(Neos.Neos:ContentComponent) {

    url = ${q(node.properties.record).property('url')}
    title = ${q(node.properties.record).property('title')}
    body = ${q(node.properties.record).property('body')}
    thumbnailUrl = ${q(node.properties.record).property('thumbnailUrl')}
    identifier = ${'embed-form-' + q(node).property('_identifier')}
    datePublished = ${q(node.properties.record).property('publishedAt')}
    itemType = ${q(node.properties.record).property('itemType')}

    renderer = Neos.Fusion:Case {
        isset {
            condition = ${node.properties.record != null}
            renderer = Ds.BetterEmbed:ComponentRenderer
        }

        backend {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:Join {
                message =  afx`
                    <div class="better-embed-placeholder">
                        <div class="heading">Please set a url in the inspector panel</div>
                    </div>
                `
            }

        }
    }
}

prototype(Ds.BetterEmbed:ComponentRenderer) < prototype(Neos.Fusion:Case) {
    default {
        condition = true
        renderer = Ds.BetterEmbed:Component.Default {
            @apply.props = ${props}
        }
    }
}


